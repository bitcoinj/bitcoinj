/*
 * Settings file for Gradle 7.3 or later.
 * For building with Debian Gradle 4.4, use the settings-debian.gradle settings.
 */

import org.gradle.util.GradleVersion

// Minimum Gradle version for build
def minGradleVersion = GradleVersion.version("7.3")

rootProject.name = 'bitcoinj-parent'

if (GradleVersion.current() < minGradleVersion) {
    throw new GradleScriptException("bitcoinj build requires Gradle ${minGradleVersion.version} or later", null)
}
if (!JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)) {
    throw new GradleScriptException("bitcoinj build requires Java 17 or later", null)
}

include 'test-support'
project(':test-support').name = 'bitcoinj-test-support'

include 'base'
project(':base').name = 'bitcoinj-base'

include 'core'
project(':core').name = 'bitcoinj-core'

include 'tools'
project(':tools').name = 'bitcoinj-tools'

include 'wallettool'
project(':wallettool').name = 'bitcoinj-wallettool'

include 'examples'
project(':examples').name = 'bitcoinj-examples'

if (JavaVersion.current().getMajorVersion().toInteger() >= 21) {
    include 'wallettemplate'
    project(':wallettemplate').name = 'bitcoinj-wallettemplate'
} else {
    System.err.println "Skipping wallettemplate, requires Java 21+, currently running Java ${JavaVersion.current()}"
}

include 'integration-test'
project(':integration-test').name = 'bitcoinj-integration-test'

include 'examples-kotlin'
project(':examples-kotlin').name = 'bitcoinj-examples-kotlin'
