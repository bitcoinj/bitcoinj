import net.ltgt.gradle.errorprone.CheckSeverity

plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.1.5'
    id "net.ltgt.errorprone" version "5.0.0"
}

dependencies {
    implementation project(':bitcoinj-core')
    implementation 'com.google.guava:guava:33.5.0-jre'
    implementation 'com.google.zxing:core:3.5.3'
    implementation 'org.jspecify:jspecify:1.0.0'
    implementation 'org.slf4j:slf4j-api:2.0.16'

    runtimeOnly 'org.slf4j:slf4j-jdk14:2.0.16'

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.11.4"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.11.4"

    annotationProcessor "com.uber.nullaway:nullaway:0.13.1"
    testAnnotationProcessor "com.uber.nullaway:nullaway:0.13.1"

    errorprone "com.google.errorprone:error_prone_core:2.47.0"
}

configurations.configureEach {
    resolutionStrategy {
        force 'com.google.guava:guava:33.5.0-jre'
    }
}

javafx {
    version = '25.0.1'
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.release = 25
    options.compilerArgs << '-Xlint:deprecation'
    options.errorprone {
        check("NullAway", CheckSeverity.ERROR)
        option("NullAway:OnlyNullMarked")
    }
}

javadoc.options.encoding = 'UTF-8'

application {
    mainClass = "wallettemplate.Main"
    mainModule = "wallettemplate"
}

jlink {
    options = ['--add-modules', 'org.slf4j.jul']
    mergedModule {
        requires 'java.logging'
        requires 'jdk.unsupported'

        requires 'org.slf4j'
    }
}

test {
    useJUnitPlatform()
}
