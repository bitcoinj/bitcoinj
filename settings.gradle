import org.gradle.util.GradleVersion
import org.gradle.api.GradleScriptException

// Minimum Gradle version for main build
def minGradleVersion = GradleVersion.version("4.4")
// Minimum Gradle version for builds of JavaFX 11 module
def minFxGradleVersion = GradleVersion.version("4.10")

rootProject.name = 'bitcoinj-parent'

if (GradleVersion.current().compareTo(minGradleVersion) < 0) {
    throw new GradleScriptException("bitcoinj build requires Gradle ${minGradleVersion} or later", null)
}

include 'core'
project(':core').name = 'bitcoinj-core'

include 'tools'
project(':tools').name = 'bitcoinj-tools'

include 'examples'
project(':examples').name = 'bitcoinj-examples'

if (GradleVersion.current().compareTo(minFxGradleVersion) >= 0 && JavaVersion.current().isJava11Compatible()) {
    System.err.println "Including bitcoinj-walletfx because ${GradleVersion.current()} and Java ${JavaVersion.current()}"
    include 'bitcoinj-walletfx'
    include 'bitcoinj-walletfx-template'
} else {
    System.err.println "Skipping bitcoinj-walletfx, requires ${minFxGradleVersion}+ and Java 11+, currently running: ${GradleVersion.current()} and Java ${JavaVersion.current()}"
}

