import org.gradle.util.GradleVersion

plugins {
    id 'java-library'
    id 'com.google.protobuf'
    id 'maven-publish'
    id 'eclipse'
}

version = '0.17-SNAPSHOT'

dependencies {
    api 'org.bouncycastle:bcprov-jdk15to18:1.70'
    api 'com.google.guava:guava:31.0.1-android'
    api 'com.google.protobuf:protobuf-javalite:3.18.0'
    api 'com.squareup.okhttp3:okhttp:3.14.9'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'net.jcip:jcip-annotations:1.0'
    compileOnly 'org.fusesource.leveldbjni:leveldbjni-all:1.8'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.easymock:easymock:4.3'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
    testImplementation 'org.slf4j:slf4j-jdk14:1.7.36'
    testImplementation 'com.h2database:h2:1.3.176'
    testImplementation 'org.fusesource.leveldbjni:leveldbjni-all:1.8'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.9'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
}

sourceCompatibility = 8
targetCompatibility = 8
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

compileJava {
    options.compilerArgs.addAll(['--release', '8'])
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.18.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
    generatedFilesBaseDir = new File(projectDir, '/src') // workaround for '$projectDir/src'
}

tasks.withType(Test) {
    exclude 'org/bitcoinj/core/PeerTest*'
    exclude 'org/bitcoinj/core/TransactionBroadcastTest*'
    exclude 'org/bitcoinj/net/discovery/DnsDiscoveryTest*'
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

// Test with default Java toolchain
test {
    doFirst {
        logger.lifecycle("Testing with default toolchain")
    }
}

def gradleVersionToolchains = GradleVersion.version("6.7")

if (GradleVersion.current().compareTo(gradleVersionToolchains) > 0) {
    // If the Gradle Java Toolchains feature is available, run tests on older JDKs
    System.err.println "Adding 'testOnJdk8' task, because ${GradleVersion.current()}"

    task('testOnJdk8', type: Test) {
        doFirst {
            logger.lifecycle("Testing with JDK ${javaLauncher.get().metadata.javaRuntimeVersion}")
        }
        javaLauncher = javaToolchains.launcherFor {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }
    // Activate if `testJdk8` is `true` in `gradle.properties` or `-PtestJdk8=true` is on command-line
    if (Boolean.valueOf(findProperty('testJdk8'))) {
        check.dependsOn testOnJdk8
    }
}

ext.moduleName = 'org.bitcoinj.core'

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes 'Automatic-Module-Name': moduleName
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'bitcoinj-core'
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                description = 'A Java Bitcoin library'
            }
        }
    }
}
