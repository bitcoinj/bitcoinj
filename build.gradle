import org.gradle.util.GradleVersion

buildscript {
    repositories {
        mavenCentral()
    }
    // If using Gradle 7, use the compatible protobuf plugin, else use the one that works with oldest supported Gradle
    boolean isGradle7 = GradleVersion.current() >= GradleVersion.version("7.0")
    def gradleProtobufVersion = isGradle7 ? "0.9.5" : "0.8.10"
    if (isGradle7) {
        System.err.println "Warning: Using com.google.protobuf:protobuf-gradle-plugin:${gradleProtobufVersion} because ${GradleVersion.current()}"
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:${gradleProtobufVersion}"
    }
}

plugins {
    id "net.ltgt.errorprone" version "5.0.0" apply false
}

allprojects { proj ->
    repositories {
        mavenCentral()
    }

    group = 'org.bitcoinj'

    def supportsErrorProne = GradleVersion.current() >= GradleVersion.version("8.5") && JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_21)
    def checkedProjects = [
            'test-support',
            'base',
            // Core is in another PR
            'tools',
            // WalletTool is in another PR
            'examples',
            'wallettemplate',
            'integration-test',
            'examples-kotlin'].collect { 'bitcoinj-' + it }
    def enableErrorProne = supportsErrorProne && checkedProjects.contains(proj.name)

    if (enableErrorProne) {
        apply plugin: 'java';
        apply plugin: 'net.ltgt.errorprone'

        dependencies {
            annotationProcessor "com.uber.nullaway:nullaway:0.13.1"
            testAnnotationProcessor "com.uber.nullaway:nullaway:0.13.1"
            errorprone "com.google.errorprone:error_prone_core:2.47.0"
        }

        tasks.withType(JavaCompile) {
            options.errorprone {
                check("NullAway", net.ltgt.gradle.errorprone.CheckSeverity.ERROR)
                option("NullAway:OnlyNullMarked")
            }
        }
    }

    // Ensure standard artifacts in all projects are built reproducibly

    tasks.withType(AbstractArchiveTask) {
        // Gradle 9+ has consistent timestamps and file order in archives by default
        boolean hasConsistentArchives = GradleVersion.current() >= GradleVersion.version("9.0")
        if (!hasConsistentArchives) {
            preserveFileTimestamps = false
            reproducibleFileOrder = true
        }
    }

    tasks.withType(Jar) {
        // Gradle 9+ has consistent permissions in jars by default
        boolean hasConsistentArchives = GradleVersion.current() >= GradleVersion.version("9.0")
        if (!hasConsistentArchives) {
            dirMode = 0755
            fileMode = 0644
        }
    }

    tasks.withType(Javadoc) {
        options.noTimestamp = true
    }
}
